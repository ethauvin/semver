plugins {
    id 'checkstyle'
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'pmd'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'net.thauvin.erik.gradle.semver' version '1.0.4'
    id 'com.github.spotbugs' version '4.4.4'
    id 'org.sonarqube' version '3.0'
}

import com.github.spotbugs.snom.SpotBugsTask
import org.apache.tools.ant.taskdefs.condition.Os

defaultTasks 'check'

group = 'net.thauvin.erik'

final def mavenName = 'SemVer'
final def mavenDescription = 'Semantic Version Annotation Processor'
final def mavenUrl = 'https://github.com/ethauvin/semver'
final def mavenLicense = 'The BSD 3-Clause License'
final def mavenLicenseUrl = 'http://opensource.org/licenses/BSD-3-Clause'
final def mavenScmCon = 'https://github.com/ethauvin/semver.git'
final def mavenScmDevCon = 'git@github.com:ethauvin/semver.git'

final def pkgLicenses = ['BSD 3-Clause']
final def pkgIssueTrackerUrl = mavenUrl + '/issues'
final def pkgLabels = ['java', 'kotlin', 'annotation', 'processor', 'semantic', 'version']

ext.versions = [
    spotbugs: '4.0.6'
]

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'com.github.spullara.mustache.java:compiler:0.9.6'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'
    spotbugsPlugins 'com.mebigfatguy.sb-contrib:sb-contrib:7.4.7'

    compileOnly "com.github.spotbugs:spotbugs-annotations:$versions.spotbugs"
    testCompileOnly "com.github.spotbugs:spotbugs-annotations:$versions.spotbugs"

    testImplementation 'org.testng:testng:7.2.0'
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

pmd {
    ignoreFailures = true
    ruleSets = []
    ruleSetFiles = files("${projectDir}/config/pmd.xml")
    consoleOutput = true
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    dryRun = false
    pkg {
        repo = 'maven'
        name = mavenName
        licenses = pkgLicenses
        desc = mavenDescription
        //released  = new Date()
        websiteUrl = mavenUrl
        issueTrackerUrl = pkgIssueTrackerUrl
        vcsUrl = mavenScmCon
        labels = pkgLabels
        publicDownloadNumbers = true
        version {
            gpg { sign = true }
        }
    }
}

bintrayUpload {
    versionName = "$project.version"
    versionDesc = "$mavenName $project.version"
    versionVcsTag = "$project.version"
    versionReleased = new Date()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    group = 'Build'
    description = 'Builds an archive of the javadoc docs.'
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    group = 'Build'
    description = 'Builds an archive of the source code.'
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourceJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            groupId project.group
            artifactId rootProject.name

            pom {
                name = mavenName
                description = mavenDescription
                url = mavenUrl
                licenses {
                    license {
                        name = mavenLicense
                        url = mavenLicenseUrl
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'ethauvin'
                        name = 'Erik C. Thauvin'
                        email = 'erik@thauvin.net'
                    }
                }
                scm {
                    connection = "scm:git:" + mavenScmCon
                    developerConnection = "scm:git:" + mavenScmDevCon
                    url = mavenScmCon
                }
            }
        }
    }
}

javadoc {
    doFirst {
        title = "$mavenDescription $project.version API"
    }
    options.with {
        tags = ['created']
        author = true
        //addBooleanOption('html4', true)
        links('https://docs.oracle.com/javase/8/docs/api/')
        //addStringOption('Xdoclint:none', '-quiet')
    }
}

test {
    useTestNG()
}

spotbugs {
    toolVersion = versions.spotbugs
    excludeFilter = file("$projectDir/config/spotbugs/excludeFilter.xml")
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task release(dependsOn: ['wrapper', 'clean', 'publishToMavenLocal']) {
    group = 'Publishing'
    description = 'Releases new version to local maven repository.'
    doFirst {
        println("Version: $version")
    }
}

task pandoc(type: Exec) {
    group = 'Documentation'
    def pandoc_args = ['--from', 'gfm',
                       '--to', 'html5',
                       '--metadata', "pagetitle=$mavenDescription",
                       '-s',
                       '-c', 'github-pandoc.css',
                       '-o', 'docs/README.html',
                       'README.md']
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine(['cmd', '/c', 'pandoc'] + pandoc_args)
    } else {
        executable 'pandoc'
        args pandoc_args
    }
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
    }

}

sonarqube {
    properties {
        property("sonar.projectKey", "ethauvin_semver")
        property("sonar.sourceEncoding", "UTF-8")
    }
}

tasks.sonarqube {
    dependsOn("jacocoTestReport")
}
