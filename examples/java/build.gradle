plugins {
    id 'java'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.21.0'
}

// ./gradlew run
// ./gradlew runExample

defaultTasks 'run'

final def semverProcessor = 'net.thauvin.erik:semver:1.2.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.annotationProcessorGeneratedSourcesDirectory = file("${projectDir}/src/generated/java")

mainClassName = 'com.example.App'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    annotationProcessor semverProcessor
    implementation semverProcessor
}

tasks.withType(JavaCompile) {
    if (JavaVersion.current().isJava12Compatible()) {
        options.compilerArgs += [ "-Asemver.project.dir=$projectDir" ]
    }
}

task runExample(type: JavaExec) {
    group = 'application'
    classpath = sourceSets.main.runtimeClasspath

    main = 'com.example.Example'
}
